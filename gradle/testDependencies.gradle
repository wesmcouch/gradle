/*
 * Copyright 2018 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */


// Test classpath libraries
libraries.spock =   'org.spockframework:spock-core:1.0-groovy-2.4'
libraries.jsoup =   'org.jsoup:jsoup:1.6.3'
libraries.xmlunit = 'xmlunit:xmlunit:1.3'
libraries.jetty =   'org.mortbay.jetty:jetty:6.1.26'
libraries.sshd =    'org.apache.sshd:sshd-core:1.2.0'
libraries.jmock = [
    'org.hamcrest:hamcrest-core',
    'org.hamcrest:hamcrest-library:1.3',
    'org.jmock:jmock:2.5.1',
    'org.jmock:jmock-junit4:2.5.1',
    'org.jmock:jmock-legacy:2.5.1'
]

allprojects {
    configurations.whenObjectAdded {
        if (name == 'testImplementation') {
            dependencies {
                constraints {
                    testImplementation('cglib:cglib-nodep') {
                        version { prefer '3.2.5' }
                        because 'We need to upgrade to a Java8 compatible version'
                    }
                }
            }
        }
    }
    dependencies {
        components {
            withModule('org.spockframework:spock-core') {
                allVariants {
                    withDependencyConstraints {
                        it.findAll { it.group == 'org.objenesis' }.each {
                            it.version { prefer '1.2' }
                            it.because '1.2 is required by Gradle and part of the distribution'
                        }
                    }
                }
            }
        }
    }
}
